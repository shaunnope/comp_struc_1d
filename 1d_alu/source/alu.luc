/**
ALUFN signals
00 0000: ADD
00 0001: SUB
00 0010: MUL
00 0011: DIV
01 1000: AND
01 1110: OR
01 0110: XOR
01 1010: "A"
01 1100: "B"
01 ZYXW: Bool with truth table 00 - W, 01 - X, 10 - Y, 11 - Z
10 0000: SHL
10 0001: SHR
10 0011: SRA
10 0100: ROTL
10 0101: ROTR
11 0011: CMPEQ
11 0101: CMPLT
11 0111: CMPLE
*/
module alu (
    input alufn[6],  //
    input x[16],     //
    input y[16],     //
    output zvnpc[5], // flags: cout only determined by ADD SUB MULT DIV 
    output out[16]
  ) {
  sig w[17];         // output w cout
  // components
  sixteen_bit_full_adder adder;
  sixteen_bit_compare compare;
  sixteen_bit_bool boole;
  sixteen_bit_mult mult;
  sixteen_bit_shifter shifter;

  op_flags flags;
  always {   
    // init adder
    adder.alufn = alufn[0];
    adder.x = x;
    adder.y = y;
    
    // init compare
    compare.alufn = alufn[2:1];
    compare.z = adder.z;
    compare.v = adder.v;
    compare.n = adder.n;
    
    // init mult
    mult.alufn = alufn[0];
    mult.x = x;
    mult.y = y;
    
    // init boole
    boole.alufn = alufn[3:0];
    boole.x = x;
    boole.y = y;
    
    // init shifter
    shifter.alufn = alufn[2:0];
    shifter.x = x;
    shifter.shift = y[3:0];
    
    w = 17b0;
    
    // select output
    case(alufn[5:4]) {
      b00: w = adder.out;
      b01: w = boole.out;
      b10: w = shifter.out;
      b11: w = compare.out;
      default: w = adder.out;
    }
    
    if(alufn[5:1] == b00001) {
      w = mult.out;
    }
    //init flags
    flags.x = x;
    flags.y = y;
    flags.alufn = alufn;
    flags.aluout = w[15:0];
    
    zvnpc[0] = flags.z;
    zvnpc[1] = flags.v;
    zvnpc[2] = flags.n;
    zvnpc[3] = flags.p;
    zvnpc[4] = w[16];
    
    out = w[15:0];
  }
}
