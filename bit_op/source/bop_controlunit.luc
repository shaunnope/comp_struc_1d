module bop_controlunit (
    input clk,  // clock
    input rst,  // reset
    input tick, // stage transition
    input slwclk,   // Used for stages
    
    input inputs[3], // 3 buttons
    
    input start,     // start rising edge
    input toggle,    // toggle rising edge
    input level[2],  // 2 bit to represent 3 levels, 01 = level 1
    
    input flags[5], // zvnpc

    output alufn[5],
    output asel, 
    output bsel[2],
    output werf,
    output wdsel,
    output lit[8],
    output rc[3],
    output ra[3],
    output rb[3],
    output bzsel, // Used for sound buzzer
    output s_rst, // soft reset
    output t_rst  // slwclk reset
  ) {
  
  .clk(clk){
    edge_detector clk_edge[4] (#RISE(1), #FALL(0));

    .rst(rst) {
      dff aluflags[5];
    }

    fsm game_fsm = {  
      START,
      
      INC_LEVEL,
      AWAIT,
      
      // For round & input purposes
      IDLE,
      INPUT_STORE,
      INPUT_IDLE,
      INPUT_RST,
      CHECK_ACTION,
      UPDATE_LSCORE,
      UPDATE_OSCORE,
      
      // For scoring systems
      L1_SCORE_ADD,
      
      L2_SCORE_MUL,
      L2_LSCORE_SHL,
      L2_SCORE_ADD,
      
      OSCORE_CHECK,
      OSCORE_BRANCH,
      SET_OSCORE,      
      
      L3_SCORE_ROTL,
      
      // Handling if end of round
      INC_TIMER,
      CHECK_ROUND_END,
      BRANCH_ROUND_END,
      
      // Handling if end of game
      RESET_CORRECT,
      UPDATE_SCORE,
      RESET_LSCORE,
      CHECK_LEVEL,
      BRANCH_LEVEL,
      AWAIT_START,
      RESET_TIMER,

      RESET_LEVEL,      
      GAME_END,
      
      // Testing sound purposes
      FP_IDLE
    }; 
  }

  // ALUFN[4:0] operations
  const ADD = 5b00000;
  const MUL = 5b00010;
  const SHL = 5b00100;
  const ROTL = 5b00110;
  const CMPEQ = 5b01011;
  const CMPLT = 5b01101;
  const A = 5b11010;
  const B = 5b11100;

  const SCORE = b000;
  const INPUT = b001;
  const TIMER = b010;
  const LEVEL = b011;
  const LSCORE = b100;
  const OSCORE = b101;
  const CORRECT = b110;
  const R7 = b111;

  sig str;
  sig stf;
  
  always {
    clk_edge.in = c{tick, ~tick, slwclk, ~slwclk};
    str = clk_edge.out[1];
    stf = clk_edge.out[0];
    
    // Standard setting unless overwritten by cases
    alufn = 0;
    asel = 0;
    bsel = 0;
    werf = 0;
    wdsel = 0;
    lit = 0;
    rc = 0;
    ra = 0;
    rb = 0;
    bzsel = 0;
    s_rst = 0;
    t_rst = 0;

    if (rst) {
      game_fsm.d = game_fsm.START;
    } 
    else {
      case(game_fsm.q) {
        game_fsm.START:
          if(start){
            s_rst = 1;
            game_fsm.d = game_fsm.INC_LEVEL;
          } else if (toggle) {
            game_fsm.d = game_fsm.FP_IDLE;
          }

        game_fsm.INC_LEVEL:
          alufn = ADD;
          asel = b0;
          bsel = b01;
          werf = b1;
          lit = 8h01;
          ra = LEVEL;
          rc = LEVEL;
          wdsel = b0;
          bzsel = 0;

          game_fsm.d = game_fsm.IDLE;
          
        game_fsm.AWAIT:
          if (stf) {
            game_fsm.d = game_fsm.IDLE;
          }

        game_fsm.IDLE:
        bzsel = b1;
        
        if (|inputs) {
          game_fsm.d = game_fsm.INPUT_STORE;
        } else if (str) {
          game_fsm.d = game_fsm.INPUT_RST;
        } else if (stf) {
          game_fsm.d = game_fsm.CHECK_ACTION;
        } else if (toggle) {
          s_rst = 1;
          game_fsm.d = game_fsm.START;
        }
        
     game_fsm.INPUT_STORE:
       werf = b1;
       rc = INPUT;
       wdsel = b1;
       game_fsm.d = game_fsm.INPUT_IDLE;
        
     game_fsm.INPUT_IDLE:
       if (str) {
         game_fsm.d = game_fsm.INPUT_RST;
       } else if (stf) {
         game_fsm.d = game_fsm.CHECK_ACTION;
       } else if (toggle) {
         s_rst = 1;
         game_fsm.d = game_fsm.START;
       }
        
      game_fsm.INPUT_RST:
        alufn = B;
        bsel = b01;
        werf = b1;
        lit = 8h0;
        rc = INPUT;
        wdsel = b0;
        game_fsm.d = game_fsm.IDLE;
        
      game_fsm.CHECK_ACTION:
        alufn = CMPEQ;
        asel = b0;
        bsel = b10;
        werf = b1;
        ra = INPUT;
        rc = CORRECT;
        wdsel = b0;
        aluflags.d = flags;
        game_fsm.d = game_fsm.UPDATE_LSCORE;
        
      game_fsm.UPDATE_LSCORE:
        alufn = ADD;
        asel = b0;
        bsel = b01;
        werf = b1;
        lit = c{7x{aluflags.q[4]}, b1};
        ra = LSCORE;
        rc = LSCORE;
        wdsel = b0;
        aluflags.d = flags;
        game_fsm.d = game_fsm.UPDATE_OSCORE;

        game_fsm.UPDATE_OSCORE:
          bzsel = b0;
          
          case(level) {
            b01: game_fsm.d = game_fsm.L1_SCORE_ADD;
            b10:
              if(aluflags.q[4] & 1) {
                game_fsm.d = game_fsm.INC_TIMER;
              } else if(aluflags.q[2] & 1) {
                game_fsm.d = game_fsm.L2_LSCORE_SHL;
              } else {
                game_fsm.d = game_fsm.L2_SCORE_MUL;
              }
            b11:
              game_fsm.d = game_fsm.L3_SCORE_ROTL;
            default: game_fsm.d = game_fsm.START;
          }

        game_fsm.L1_SCORE_ADD:
          alufn = ADD;
          asel = b0;
          bsel = b00;
          werf = b1;
          ra = SCORE;
          rb = LSCORE;
          rc = OSCORE;
          wdsel = b0;
          bzsel = b0;
          aluflags.d = flags;

          game_fsm.d = game_fsm.OSCORE_CHECK;
                  
        game_fsm.L2_SCORE_MUL:
          alufn = MUL;
          asel = b0;
          bsel = b00;
          werf = b1;
          ra = SCORE;
          rb = LSCORE;
          rc = OSCORE;
          wdsel = b0;
          bzsel = b0;

          game_fsm.d = game_fsm.INC_TIMER;

        game_fsm.L2_LSCORE_SHL:
          alufn = SHL;
          asel = b0;
          bsel = b01;
          lit = 8h01;
          werf = b1;
          ra = LSCORE;
          rc = R7;
          wdsel = b0;
          bzsel = b0;

          game_fsm.d = game_fsm.L2_SCORE_ADD;
          
        game_fsm.L2_SCORE_ADD:
          alufn = ADD;
          asel = b0;
          bsel = b00;
          werf = b1;
          ra = SCORE;
          rb = R7;
          rc = OSCORE;
          wdsel = b0;
          bzsel = b0;
          aluflags.d = flags;
          
          game_fsm.d = game_fsm.OSCORE_CHECK;
          
       game_fsm.OSCORE_CHECK:
          bzsel = b0;
          
          if (aluflags.q[4] | aluflags.q[2]) {
            game_fsm.d = game_fsm.SET_OSCORE;
          } else {
            game_fsm.d = game_fsm.INC_TIMER;
          }
          
       game_fsm.SET_OSCORE:
          alufn = A;
          asel = b1;
          werf = b1;
          lit = 8h01;
          rc = OSCORE;
          wdsel = b0;
          bzsel = b0;
          
          game_fsm.d = game_fsm.INC_TIMER;

        game_fsm.L3_SCORE_ROTL:
          alufn = ROTL;
          asel = b0;
          bsel = b00;
          werf = b1;
          ra = SCORE;
          rb = LSCORE;
          rc = OSCORE;
          wdsel = b0;
          bzsel = b0;

          game_fsm.d = game_fsm.INC_TIMER;

        game_fsm.INC_TIMER:
          alufn = ADD;
          asel = b0;
          bsel = b01;
          werf = b1;
          lit = 8h01;
          ra = TIMER;
          rc = TIMER;
          wdsel = b0;
          bzsel = b0;

          game_fsm.d = game_fsm.CHECK_ROUND_END;

        game_fsm.CHECK_ROUND_END:
          alufn = B;
          bsel = b11;
          werf = b1;
          rc = R7;
          wdsel = b0;
          bzsel = b0;
          aluflags.d = flags;
          game_fsm.d = game_fsm.BRANCH_ROUND_END;

        game_fsm.BRANCH_ROUND_END:
          werf = b0;
          rb = R7;
          bzsel = b0;

          if (aluflags.q[4] & 1) {
            game_fsm.d = game_fsm.IDLE;
          } else {
            game_fsm.d = game_fsm.RESET_CORRECT;
          }

        game_fsm.RESET_CORRECT: // sets CORRECT to 1: green LEDs
          alufn = B;
          bsel = b01;
          werf = b1;
          lit = 8h01;
          rc = CORRECT;
          wdsel = b0;
          bzsel = b0;

          game_fsm.d = game_fsm.UPDATE_SCORE;
          
        game_fsm.UPDATE_SCORE: // copy OSCORE to SCORE
          alufn = A;
          asel = b0;
          werf = b1;
          ra = OSCORE;
          rc = SCORE;
          wdsel = b0;
          bzsel = b0;
        
          game_fsm.d = game_fsm.RESET_LSCORE;

        game_fsm.RESET_LSCORE:
          alufn = B;
          bsel = b01;
          werf = b1;
          lit = 8h00;
          rc = LSCORE;
          wdsel = b0;
          bzsel = b0;
          game_fsm.d = game_fsm.CHECK_LEVEL;

        game_fsm.CHECK_LEVEL: // check LEVEL < 3
          alufn = CMPLT;
          asel = b0;
          bsel = b01;
          werf = b1;
          lit = 8h03;
          ra = LEVEL;
          rc = R7;
          wdsel = b0;
          bzsel = b0;
          aluflags.d = flags;
          game_fsm.d = game_fsm.BRANCH_LEVEL;

        game_fsm.BRANCH_LEVEL:
          werf = b0;
          rb = R7;
          bzsel = b0;

          if (aluflags.q[4] & 1) {
            game_fsm.d = game_fsm.RESET_LEVEL;
          } else {
            game_fsm.d = game_fsm.AWAIT_START;
          }
          
        game_fsm.AWAIT_START:
          if (start) {
            t_rst = 1;
            game_fsm.d = game_fsm.RESET_TIMER;
          } else if (toggle) {
            s_rst = 1;
            game_fsm.d = game_fsm.START;
          }

        game_fsm.RESET_TIMER:
          alufn = B;
          bsel = b01;
          werf = b1;
          lit = 8h00;
          rc = TIMER;
          wdsel = b0;
          bzsel = b0;

          game_fsm.d = game_fsm.INC_LEVEL;

        game_fsm.GAME_END:
          alufn = B;
          bsel = b01;
          werf = b1;
          lit = 8h00;
          rc = LEVEL;
          wdsel = b0;
          bzsel = b0;
          
          game_fsm.d = game_fsm.START;
 
        game_fsm.FP_IDLE:
          werf = b0;
          bzsel = b1;

          if (toggle) {
            game_fsm.d = game_fsm.START;
          }
          
        default:
          game_fsm.d = game_fsm.START;
    }

  }
